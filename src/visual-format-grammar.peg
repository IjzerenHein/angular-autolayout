{
  function extend(dst) {
    for (var i = 1; i < arguments.length; i++) {
      for (var k in arguments[i]) {
        dst[k] = arguments[i][k];
      }
    }
    return dst;
  }
}

visualFormatString
  = o:(orientation ":")?
    superto:(superview connection)?
    view:view
    views:(connection view)*
    tosuper:(connection superview)?
  { return {
      orientation: o ? o[0] : 'horizontal',
      cascade: (superto || []).concat(
        [view],
        [].concat.apply([], views),
        (tosuper || [])
      )
    }
  }

orientation
  = orient:("H" / "V") { return orient == 'H' ? 'horizontal' : 'vertical' }

superview
  = "|" { return { view: null } }

view
  = "[" view:viewName predicates:predicateListWithParens? "]"
  { return extend(view, predicates ? { constrains: predicates } : {}) }

connection
  = "-" predicateList:predicateList "-" { return predicateList }
  / "-" { return [{ relation: 'equal', constant: 'default' }] }
  / "" { return [{ relation: 'equal', constant: 0 }] }

predicateList
  = simplePredicate
  / predicateListWithParens

simplePredicate
  = n:number { return [{ relation: 'equal', constant: n }] }

predicateListWithParens
  = "(" p:predicate ps:("," predicate)* ")"
  { return [p].concat(ps.map(function(p){ return p[1] })) }

predicate
  = r:relation? o:objectOfPredicate p:("@" priority)?
  { return extend({ relation: 'equal' }, (r || {}), o, (p ? p[1]: {})) }

relation
  = "==" { return { relation: 'equal' } }
  / "<=" { return { relation: 'lessOrEqual' } }
  / ">=" { return { relation: 'greaterOrEqual' } }

objectOfPredicate
  = constant
  / viewName

priority
  = n:number { return { priority: n } }

constant
  = n:number { return { constant: n } }

viewName
  = v:$[a-zA-Z0-9#\.]+ { return { view: v } }

number
  = digits:[0-9]+ { return parseInt(digits.join(""), 10); }